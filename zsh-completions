#compdef poudriere

_poudriere_jail() {
	_values "poudriere jails" ${${(f)"$(poudriere jails -lq)"}%% *}
}

_poudriere_pt() {
	_values "poudriere portstrees" ${${(f)"$(ls /usr/local/poudriere/ports/(/))"}%%.*}
}

# shamelessely stolen from
# /usr/local/share/zsh/4.3.12/functions/Completion/BSD/_portmaster
_poudriere_ports() {
  local ret=1 _fbsd_ports _fbsd_cat
   _fbsd_cat=(${PORTSDIR:-/usr/local/poudriere/ports/default/ports}/[a-z]*(/:t))
   if [[ $PREFIX != */* ]] ; then
     _wanted cat_packages expl 'category/ports' compadd -S '/' $_fbsd_cat
   else
     compset -P '*/'
     _fbsd_ports=(${PORTSDIR:-/usr/local/poudriere/ports/default/ports}/$IPREFIX/*(/:t))
     _wanted cat_packages expl 'category/ports' compadd $_fbsd_ports
   fi
  return ret
}

_bulk=(
	'-f[Give the list of ports to build]:name of file:_files'
#Options:
	'-k[Keep the previous built binary packages]::'
	'*-j[Run only on the given jail]::jail name:_poudriere_jail'
	'-p[Specify on which ports tree the bulk will be done]::tree:_poudriere_pt'
)

_genpkg=(
	'-d[Relative path of the port we want to build]:port:_files -/'
	'-o[Specify an origin in the portstree]:origin:_files -/'
#Options:
	'*-j[Run only inside the given jail]::name:_poudriere_jail'
	'-p[Use portstree "tree"]::tree:_poudriere_pt'
)

_jail=(
	'-c[create a jail]'
	'-d[delete a jail]'
	'-l[list all available jails]'
	'-s[start a jail]'
	'-k[kill (stop) a jail]'
#Options:
	'-q[quiet (remove the header in list)]::'
	'*-j[Specifies the jailname]::jailname:_poudriere_jail'
	'-v[Specifies which version of FreeBSD we want in jail]::version'
	'-a[Indicates architecture of the jail: i386 or amd64(same as host by default)]::arch:(amd64 i386)'
	'-m[Method used to create jail, specify NONE if you want to use your home made jail(ftp by default)]::method'
	'-f[FS name (tank/jails/myjail)]::fs:_files -/'
	'-M[mountpoint]::mountpoint:_files -/'
)

_testport=(
	'-d[specify on which port we work]:path:_poudriere_ports'
	'-o[specify an origin in the portstree]:origin:_files -/'
# options
	'-c[run make config for the given port]::'
	'*-j[run only inside the given jail]::name:_poudriere_jail'
	'-n[no custom prefix]::'
	'-p[specify on which portstree we work]::tree:_poudriere_pt'
)


_poudriere () {
	local cmds
	local context state line
	typeset -A val_args
	cmds=(
		'bulk:generate packages for given ports'
		'genpkg:generate package for a given port'
		'jail:manage the jails used by poudriere'
		'ports:create, update or delete the portstrees used by poudriere'
		'testport:launch a test on a given port'
	)

	if ((CURRENT == 2)); then
		_describe "poudriere main commands" cmds
	else
		case $words[2] in
			bulk)
			_arguments -s $_bulk "::" && return
			;;
			genpkg)
			_arguments -s $_genpkg "::" && return
			;;
			jail)
			_arguments -s $_jail "::" && return
			;;
			ports)
			_arguments -s $_ports "::" && return
			;;
			testport)
			_arguments -s $_testport "::" && return
			;;
		esac
	fi
}

_poudriere "$@"
